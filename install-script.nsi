; Script generated by the HM NIS Edit Script Wizard.

!include "MUI.nsh"
; Check x64 Macro
!include "x64.nsh"
; If Macro
!include "LogicLib.nsh"

  ;Name and file
  Name "Virtual Dimension"
  OutFile "VirtualDimension-Setup.exe"

  ;Program Version
  !define VERSION "0.94b"

  ;Default installation folder
  InstallDir "$PROGRAMFILES64\Virtual Dimension"

  ;Get installation folder from registry if available
  InstallDirRegKey  HKLM "Software\Microsoft\Windows\CurrentVersion\App Paths\VirtualDimension.exe" ""

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  ;Pages options
  !define MUI_LICENSEPAGE_RADIOBUTTONS
  !define MUI_STARTMENUPAGE_NODISABLE
  !define MUI_STARTMENUPAGE_DEFAULTFOLDER "Virtual Dimension"
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension"
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "NSIS:StartMenuDir"
  !define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\orange-install.ico"
  !define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\orange-uninstall.ico"

  ;Pages
  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "License.txt"
  !insertmacro MUI_PAGE_DIRECTORY
  Var STARTMENU_FOLDER
  !insertmacro MUI_PAGE_STARTMENU "VirtualDimension" $STARTMENU_FOLDER
  !insertmacro MUI_PAGE_INSTFILES
  !insertmacro MUI_PAGE_FINISH

  !insertmacro MUI_UNPAGE_WELCOME
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !insertmacro MUI_UNPAGE_FINISH

;--------------------------------
;Languages

  !insertmacro MUI_LANGUAGE "English"
  !insertmacro MUI_LANGUAGE "Japanese"
  !insertmacro MUI_LANGUAGE "French"

LicenseData "LICENSE.txt"
ShowInstDetails show
ShowUnInstDetails show

;--------------------------------
;Installer Sections

Section "MainSection" SEC01
  ;Regular files
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  ${If} ${RunningX64}
    File "LICENSE.html"
    File "x64\Release\HookDLL.dll"
    File "x64\Release\langEN.dll"
    File "x64\Release\langFR.dll"
    File "x64\Release\langJP.dll"
    File "x64\Release\VirtualDimension.exe"
  ${Else}
    File "LICENSE.html"
    File "Release\HookDLL.dll"
    File "Release\langEN.dll"
    File "Release\langFR.dll"
    File "Release\langJP.dll"
    File "Release\VirtualDimension.exe"
  ${EndIf}

  ;Create uninstaller
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "DisplayName" "Virtual Dimension"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "DisplayIcon" '"$INSTDIR\VirtualDimension.exe"'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "DisplayVersion" ${VERSION}
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "Publisher" "Typz Software"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "UninstallString" '"$INSTDIR\Uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension" "NoRepair" 1
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  ;SetOutPath $SYSDIR
  ;File "C:\Program Files\MinGW\bin\mingwm10.dll"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN VirtualDimension
  CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Virtual Dimension.lnk" "$INSTDIR\VirtualDimension.exe"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\License.lnk" "$INSTDIR\LICENSE.html"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
  
  ${If} ${RunningX64}
    WriteRegDWORD HKCU "Software\Typz Software\Virtual Dimension\HidingMethodsTweaks" "$PROGRAMFILES64\Internet Explorer\iexplore.exe" 1
    WriteRegDWORD HKCU "Software\Typz Software\Virtual Dimension\HidingMethodsTweaks" "$PROGRAMFILES32\Internet Explorer\iexplore.exe" 1
  ${Else}
    WriteRegDWORD HKCU "Software\Typz Software\Virtual Dimension\HidingMethodsTweaks" "$PROGRAMFILES\Internet Explorer\iexplore.exe" 1
  ${EndIf}
  WriteRegDWORD HKCU "Software\Typz Software\Virtual Dimension\HidingMethodsTweaks" "$WINDIR\explorer.exe" 1
  ;WriteRegDWORD HKCU "Software\Typz Software\Virtual Dimension\HidingMethodsTweaks" "C:\WINNT\explorer.exe" 1

SectionEnd

Section Uninstall
  Delete "$INSTDIR\LICENSE.html"
  Delete "$INSTDIR\langEN.dll"
  Delete "$INSTDIR\langFR.dll"
  Delete "$INSTDIR\langJP.dll"
  Delete "$INSTDIR\HookDLL.dll"
  Delete "$INSTDIR\VirtualDimension.exe"
  Delete "$INSTDIR\Uninstall.exe"
  ;Delete "$SYSDIR\mingwm10.dll"

  !insertmacro MUI_STARTMENU_GETFOLDER VirtualDimension $STARTMENU_FOLDER

  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\Virtual Dimension.lnk"
  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk"
  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\License.lnk"

  RMDir "$SMPROGRAMS\$STARTMENU_FOLDER"
  RMDir "$INSTDIR"

  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Virtual Dimension"

  SetAutoClose true
SectionEnd
